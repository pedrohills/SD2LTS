-- @path MM=/SD2LTS/MetaModels/SD_MetaModel.ecore
-- @path MM1=/SD2LTS/MetaModels/LTS_MetaModel.ecore

module Myrules;
create OUT : MM1 from IN : MM;

-- Helper que verifica se há estado final no SD
helper def : hasFinalState : Boolean = MM!SD.allInstances()->asSequence().at(1).hasFinal;

-- Helper que verifica se objeto iterado é o primeiro.
helper context MM!Object def: isFirst(): Boolean =
	if self.order = 1 then
		true
	else
		false
	endif;

-- Helper que contabiliza o número total de objetos
helper def : totalObjects : Integer = MM!Object.allInstances()->asSequence()->size();

-- Helper que verifica se o objeto iterado é o último.
helper context MM!Object def: isLast(): Boolean =
	if thisModule.hasFinalState and self.order = thisModule.totalObjects then
		true
	else
		false
	endif;

-- Inicialização do estado inicial e final como indefinido
helper def: initialState : MM1!InitialState = OclUndefined; 
helper def: finalState : MM1!FinalState = OclUndefined;

-- Transforma o primeiro objeto para o estado inicial
rule FirstObject2InitialState {
	from
		Object : MM!Object (Object.isFirst())	-- Faz uso do helper isFirst para filtrar o primeiro objeto do modelo de entrada
	to 
		State : MM1!InitialState (
			name <- 'InitialState ' + Object.name
		)
		do {
			thisModule.initialState = State;
		}
}

-- Transforma o último objeto para o estado final
rule LastObject2FinalState {
	from
		Object : MM!Object (Object.isLast())	-- Faz uso do helper isLast para filtrar o último objeto do modelo de entrada
	to 
		State : MM1!FinalState (
			name <- 'FinalState ' + Object.name
		)
		do {
			thisModule.finalState = State;
		}
}

-- Transforma objeto em estado
rule Object2State {
	from
		Object : MM!Object (not Object.isLast() and not Object.isFirst()) -- Usa os helpers para não filtrar o primeiro e o último obj.
	to 
		State : MM1!State (
			name <- 'State ' + Object.order.toString() + ' - ' + Object.name
		)
}